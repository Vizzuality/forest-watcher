// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        minSdkVersion = 17
        compileSdkVersion = 27
        targetSdkVersion = 26
        supportLibVersion = "27.1.1"
        glideVersion = "4.7.1"
    }
    repositories {
        google()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.google.gms:google-services:4.1.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
    }

    configurations.all {
        resolutionStrategy {
            // Ensure all support libraries are using the correct version (except multidex because that isn't on the same version train as the rest of the support libs)
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'com.android.support' && !details.requested.name.contains("multidex")) {
                    if (details.requested.version != rootProject.ext.supportLibVersion) {
                        println "Force ${details.requested.group}:${details.requested.name}:${details.requested.version} to ${rootProject.ext.supportLibVersion}"
                    }
                    details.useVersion "${rootProject.ext.supportLibVersion}"
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
    distributionUrl = distributionUrl.replace("bin", "all")
}
